(defrule ; When we arrive in the feudal age.
(current-age == feudal-age)
=>
(set-escrow-percentage food 15)
(set-escrow-percentage wood 15)
(set-escrow-percentage gold 15)
(set-escrow-percentage stone 0)
(set-goal goal-past-dark TRUE)
(disable-self)
)


(defrule ; Let's go to the next age!!
(current-age == feudal-age)
(building-type-count-total blacksmith > 0)
(building-type-count-total stable > 0)
(can-research-with-escrow castle-age)
=>
(release-escrow food)
(release-escrow gold)
(research castle-age)
(chat-to-all "Elvis will see you in the next age, baby.")
(set-strategic-number sn-food-gatherer-percentage 50)
(set-strategic-number sn-wood-gatherer-percentage 20)
(set-strategic-number sn-gold-gatherer-percentage 15)
(set-strategic-number sn-stone-gatherer-percentage 15)
)


; --------------------------------------- New Camps, throughout rest of game ------------------

(defrule
	(building-type-count farm > 15)
	(building-type-count lumber-camp > 0)
	(building-type-count-total mill < 2)
;	(idle-farm-count < 3)
	(can-build mill)
=>
	(build mill)
)
(defrule
	(building-type-count farm > 25)
	(building-type-count lumber-camp > 0)
	(building-type-count-total mill < 3)
;	(idle-farm-count < 3)
	(can-build mill)
=>
	(build mill)
)
(defrule
	(building-type-count farm > 35)
	(building-type-count lumber-camp > 0)
	(building-type-count-total mill < 4)
;	(idle-farm-count < 3)
	(can-build mill)
=>
	(build mill)
)
(defrule
	(building-type-count farm > 45)
	(building-type-count lumber-camp > 0)
	(building-type-count-total mill < 5)
;	(idle-farm-count < 3)
	(can-build mill)
=>
	(build mill)
)

(defrule
(goal goal-past-dark TRUE)
(dropsite-min-distance wood > 3)
(resource-found wood)
(can-build lumber-camp)
=>
(build lumber-camp)
)


(defrule
(goal goal-past-dark TRUE)
(dropsite-min-distance gold > 3)
(resource-found gold)
(can-build mining-camp)
=>
(build mining-camp)
)

(defrule
(goal goal-past-dark TRUE)
(dropsite-min-distance stone > 3)
(resource-found stone)
(can-build mining-camp)
=>
(build mining-camp)
)

; ----------------------------------------------------------------------------------------------

(defrule
(current-age == feudal-age)
(unit-type-count-total villager < num-feudal-age-villagers)
(can-train villager)
=>
(train villager)
)

(defrule
(goal goal-past-dark TRUE)
(building-type-count-total mining-camp > 0)
(building-type-count-total mining-camp < 2)
(resource-found stone)
(can-build mining-camp)
=>
(build mining-camp)
)

(defrule
(goal goal-past-dark TRUE)
(can-build-wall-with-escrow 2 stone-wall-line)
=>
(release-escrow stone)
(build-wall 2 stone-wall-line)
)

(defrule
(goal goal-past-dark TRUE)
(can-build-gate 2)
=>
(build-gate 2)
(disable-self)
)

(defrule ; Copy of last rule to ensure 2 gates.
(goal goal-past-dark TRUE)
(can-build-gate 2)
=>
(build-gate 2)
(disable-self)
)

; ~~~~~~~~~~~~~~ Researching for Feudal Age ~~~~~~~~~~~~~~

(defrule
(can-research-with-escrow ri-wheel-barrow)
=>
(release-escrow food)
(release-escrow wood)
(research ri-wheel-barrow)
)

(defrule
(can-research-with-escrow ri-gold-mining)
=>
(release-escrow food)
(release-escrow wood)
(research ri-gold-mining)
)

(defrule
(can-research-with-escrow ri-double-bit-axe)
=>
(release-escrow food)
(release-escrow wood)
(research ri-double-bit-axe)
)

; Let's research the essential blacksmith stuff.
(defrule
(can-research-with-escrow ri-forging)
=>
(release-escrow food)
(research ri-forging)
)

(defrule
(can-research-with-escrow ri-scale-barding)
=>
(release-escrow food)
(research ri-scale-barding)
)

; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

(defrule
(goal goal-past-dark TRUE)
(building-type-count-total blacksmith < 1)
(can-build-with-escrow blacksmith)
=>
(release-escrow wood)
(build blacksmith)
)

(defrule
(goal goal-past-dark TRUE)
(building-type-count-total stable < 1)
(can-build-with-escrow stable)
=>
(release-escrow wood)
(build stable)
)


; Some defense. -------------------

(defrule
(current-age == feudal-age)
(building-type-count-total watch-tower-line < 1)
(can-build watch-tower-line)
=>
(build watch-tower-line)
)

(defrule ; Let's keep a reserve at all times.
(current-age == feudal-age)
(unit-type-count-total spearman-line < 3)
(can-train spearman-line)
=>
(train spearman-line)
)

(defrule
(current-age == feudal-age)
(town-under-attack)
(unit-type-count-total spearman-line <= 8)
(can-train spearman-line)
=>
(train spearman-line)
)

(defrule
(goal goal-past-dark TRUE)
(town-under-attack)
(can-research-with-escrow ri-scale-mail)
=>
(release-escrow food)
(research ri-scale-mail)
)

(defrule
(goal goal-past-dark TRUE)
(can-research-with-escrow ri-fletching)
=>
(release-escrow food)
(release-escrow gold)
(research ri-fletching)
)

; ----------------------------------------------