(defconst num-feudal-age-villagers 40)
(defconst timer-new-mining-camp 1)
(defconst timer-new-mill 2)
(defconst timer-new-lumber-camp 4)

(defrule ; When we arrive in the feudal age.
(current-age == feudal-age)
=>
(set-escrow-percentage food 30)
(set-escrow-percentage wood 30)
(set-escrow-percentage gold 30)
(set-escrow-percentage stone 0)
(set-goal goal-past-dark TRUE)
(disable-self)
)


(defrule ; Let's go to the next age!!
(current-age == feudal-age)
(building-type-count-total blacksmith > 0)
(building-type-count-total stable > 0)
(unit-type-count-total villager >= num-feudal-age-villagers)
(can-research-with-escrow castle-age)
=>
(release-escrow food)
(release-escrow gold)
(research castle-age)
(chat-to-all "Elvis will see you in the next age, baby.")
(set-strategic-number sn-food-gatherer-percentage 50)
(set-strategic-number sn-wood-gatherer-percentage 20)
(set-strategic-number sn-gold-gatherer-percentage 15)
(set-strategic-number sn-stone-gatherer-percentage 15)
)


; --------------------------------------- New Camps, throughout rest of game ------------------

(defrule
(current-age == feudal-age)
=>
(enable-timer timer-new-mining-camp 1)
(disable-self)
)

(defrule
(current-age == feudal-age)
=>
(enable-timer timer-new-lumber-camp 1)
(disable-self)
)

(defrule
(current-age == feudal-age)
=>
(enable-timer timer-new-mill 1)
(disable-self)
)

(defrule
(timer-triggered timer-new-lumber-camp)
(can-build lumber-camp)
(building-type-count-total barracks > 0)
=>
(build lumber-camp)
(enable-timer timer-new-lumber-camp num-new-camp-timer)
)

(defrule
(timer-triggered timer-new-mining-camp)
(can-build mining-camp)
(building-type-count-total barracks > 0)
=>
(build mining-camp)
(enable-timer timer-new-mining-camp num-new-camp-timer)
)

(defrule
(timer-triggered timer-new-mill)
(can-build mill)
(building-type-count-total barracks > 0)
=>
(build mill)
(enable-timer timer-new-mill num-new-camp-timer)
)

; ----------------------------------------------------------------------------------------------

(defrule
(current-age == feudal-age)
(unit-type-count-total villager < num-feudal-age-villagers)
(can-train villager)
=>
(train villager)
)

(defrule
(goal goal-past-dark TRUE)
(building-type-count-total mining-camp > 0)
(building-type-count-total mining-camp < 2)
(resource-found stone)
(can-build mining-camp)
=>
(build mining-camp)
)

; ~~~~~~~~~~~~~~ Researching for Feudal Age ~~~~~~~~~~~~~~

(defrule
(can-research-with-escrow ri-wheel-barrow)
=>
(release-escrow food)
(release-escrow wood)
(research ri-wheel-barrow)
)

(defrule
(can-research-with-escrow ri-gold-mining)
=>
(release-escrow food)
(release-escrow wood)
(research ri-gold-mining)
)

(defrule
(can-research-with-escrow ri-double-bit-axe)
=>
(release-escrow food)
(release-escrow wood)
(research ri-double-bit-axe)
)

; Let's research the essential blacksmith stuff.
(defrule
(can-research-with-escrow ri-forging)
=>
(release-escrow food)
(research ri-forging)
)

(defrule
(can-research-with-escrow ri-scale-barding)
=>
(release-escrow food)
(research ri-scale-barding)
)

; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

(defrule
(goal goal-past-dark TRUE)
(building-type-count-total blacksmith < 1)
(can-build-with-escrow blacksmith)
=>
(release-escrow wood)
(build blacksmith)
)

(defrule
(goal goal-past-dark TRUE)
(building-type-count-total stable < 1)
(can-build-with-escrow stable)
=>
(release-escrow wood)
(build stable)
)


; Some defense. -------------------

(defrule
(current-age == feudal-age)
(building-type-count-total watch-tower-line < 1)
(can-build watch-tower-line)
=>
(build watch-tower-line)
)

(defrule ; Let's keep a reserve at all times.
(current-age == feudal-age)
(unit-type-count-total spearman-line < 3)
(can-train spearman-line)
=>
(train spearman-line)
)

(defrule
(current-age == feudal-age)
(town-under-attack)
(unit-type-count-total spearman-line <= 8)
(can-train spearman-line)
=>
(train spearman-line)
)

(defrule
(goal goal-past-dark TRUE)
(town-under-attack)
(can-research-with-escrow ri-scale-mail)
=>
(release-escrow food)
(research ri-scale-mail)
)

; ----------------------------------------------