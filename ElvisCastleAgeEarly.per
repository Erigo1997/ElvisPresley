(defconst num-castle-age-villagers 80)
(defconst num-knight-squad 4)

(defrule ; When we arrive in the Castle age.
(current-age == castle-age)
=>
(set-escrow-percentage food 30)
(set-escrow-percentage wood 30)
(set-escrow-percentage gold 30)
(set-escrow-percentage stone 0)
(set-goal goal-past-feudal TRUE)
(disable-self)
)

(defrule
(can-build lumber-camp)
=>
(build lumber-camp)
(disable-self)
)

(defrule
(goal goal-past-feudal TRUE)
(building-type-count-total town-center < 3)
(can-build-with-escrow town-center)
=>
(release-escrow wood)
(build town-center)
)

(defrule
(goal goal-past-feudal TRUE)
(building-type-count-total stable < 2)
(can-build-with-escrow stable)
=>
(release-escrow wood)
(build stable)
)

; Let's train some knights!.
(defrule
(current-age == castle-age)
(unit-type-count-total knight-line <= 10)
(can-train knight-line)
=>
(train knight-line)
)

; If they're ready to rock - again - let's roll!
(defrule
(not(town-under-attack))
(unit-type-count-total knight-line > num-knight-squad)
(can-build watch-tower-line)
=>
(attack-now)
(build-forward watch-tower-line)
(chat-to-all "Here comes the one for the money.")
(disable-self)
)

(defrule
(current-age == castle-age)
(unit-type-count-total villager < num-castle-age-villagers)
(can-train villager)
=>
(train villager)
)

(defrule
(goal goal-past-feudal TRUE)
(can-build castle)
=>
(build castle)
)


; Some defense. -------------------

(defrule
(current-age == castle-age)
(building-type-count-total watch-tower-line < 1)
(can-build watch-tower-line)
=>
(build watch-tower-line)
)

(defrule
(current-age == castle-age)
(town-under-attack)
(unit-type-count-total spearman-line <= 10)
(can-train spearman-line)
=>
(train spearman-line)
)


; ----------------------------------------------

; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ Castle Age Researching ~~~~~~~~~~~~~~~~~~~~~~~~~~

(defrule
(can-research-with-escrow ri-hand-cart)
=>
(release-escrow food)
(release-escrow wood)
(research ri-hand-cart)
)

(defrule
(can-research-with-escrow ri-gold-shaft-mining)
=>
(release-escrow food)
(release-escrow wood)
(research ri-gold-shaft-mining)
)

(defrule
(can-research-with-escrow ri-bow-saw)
=>
(release-escrow food)
(release-escrow wood)
(research ri-bow-saw)
)

; Let's research the essential blacksmith stuff.
(defrule
(can-research-with-escrow ri-iron-casting)
=>
(release-escrow food)
(release-escrow gold)
(research ri-iron-casting)
)

(defrule
(can-research-with-escrow ri-chain-barding)
=>
(release-escrow food)
(release-escrow gold)
(research ri-chain-barding)
)

(defrule
(goal goal-past-feudal TRUE)
(can-research-with-escrow ri-fletching)
=>
(release-escrow food)
(release-escrow gold)
(research ri-fletching)
)

(defrule
(not(town-under-attack))
(can-research-with-escrow ri-pikeman)
=>
(release-escrow food)
(release-escrow gold)
(research ri-pikeman)
)


